import org.gradle.internal.jvm.Jvm

plugins {
    id 'c'
    id 'cpp'
    id 'visual-studio'
    id 'edu.wpi.first.NativeUtils' version '2023.3.0'
}

repositories {
    mavenCentral()
}

ext.licenseFile = file("$rootDir/LICENSE.txt")

apply from: 'config.gradle'

def outputsFolder = file("$buildDir/allOutputs")

task copyAllOutputs(type: Copy) {
    destinationDir outputsFolder
}

build.dependsOn copyAllOutputs

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archivePath
    copyAllOutputs.from task.archivePath
}

model {
    components {
        apriltag(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDirs 'apriltag'
                        include '**/*.c'
                        exclude 'apriltag_pywrap.c'
                    }
                    exportedHeaders {
                        srcDirs 'apriltag'
                        include '**/*.h'
                    }
                }
                cpp {
                    source {
                        srcDirs 'apriltag'
                        include '**/*.cpp'
                    }
                }
            }
            binaries.all {
                if (toolChain in VisualCpp) {
                    // cCompiler.args '-D_UNICODE', '-DUNICODE', '-DWIN32',  '-D_WIN32', '-DSTRICT', '-DWIN32_LEAN_AND_MEAN', '-D_HAS_EXCEPTIONS=1'
                } else {
                    cCompiler.args '-w', '-fsyntax-only', "-Wno-error=format-truncation"
                    cppCompiler.args '-w', '-fsyntax-only', "-Wno-error=format-truncation"
                }

                if (targetPlatform.operatingSystem.macOsX) {
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include/darwin"
                    cppCompiler.args '-mmacosx-version-min=10.4'
                    linker.args '-mmacosx-version-min=10.4'
                } else if (targetPlatform.operatingSystem.linux) {
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include/linux"
                    cppCompiler.args '-D_FILE_OFFSET_BITS=64'
                } else if (targetPlatform.operatingSystem.windows) {
                    cppCompiler.args "-I${Jvm.current().javaHome}/include"
                    cppCompiler.args "-I${Jvm.current().javaHome}/include/win32"
                    linker.args "Shlwapi.lib", "Advapi32.lib"
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include/freebsd"
                }
            }
            binaries.withType(SharedLibraryBinarySpec) {
                buildable = false
            }
            appendDebugPathToBinaries(binaries)
        }
    }
}

apply from: 'publish.gradle'

wrapper {
    gradleVersion = '7.5.1'
}
